buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        mavenLocal()
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.7"
}

group = 'io.protostuff'
description = 'Protobuf Plugin for JetBrains IDEs'

repositories {
    jcenter()
    mavenLocal()
    maven {
        url "https://dl.bintray.com/antlr/maven/"
    }
}

dependencies {
    compile 'org.antlr:antlr4-runtime:4.7'
    compile ('org.antlr:antlr4-jetbrains-adapter:2.0.0') {
        exclude group: 'com.jetbrains'
    }
    compile 'com.google.guava:guava:21.0'
    compile 'io.protostuff:protostuff-parser:2.0.0-alpha35'
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    version = ideaVersion
    type = 'IC'
    updateSinceUntilBuild = false
    downloadSources = true
    publish {
        username = project.hasProperty('jetbrainsUser')    \
              ? project.property('jetbrainsUser')    \
              : System.getenv('JETBRAINS_USER')
        password = project.hasProperty('jetbrainsPassword')    \
              ? project.property('jetbrainsPassword')   \
              : System.getenv('JETBRAINS_PASSWORD')
        pluginId = '8277'
    }
}

apply plugin: 'java'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
